plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {

    def mApplicationName = "peerpower_test"

    defaultConfig {
        compileSdkVersion project.ext.compileSdkVersion
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionName rootProject.ext.version_name
        versionCode rootProject.ext.version_code
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled = true
        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${mApplicationName}-${variant.flavorName}-${variant.versionName}-${variant.versionCode}.apk"
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
            versionNameSuffix '-DEBUG'
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-debug.pro'
        }
        release {
            useProguard true
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    def modeDimension = 'mode'
    flavorDimensions modeDimension
    productFlavors {
        dev {
            dimension modeDimension
            applicationId "th.test.core.dev"
            versionName rootProject.ext.version_name + "-DEV"
        }

        staging {
            dimension modeDimension
            applicationId "th.test.core.staging"
            versionName rootProject.ext.version_name + "-STAGING"
        }

        prod {
            dimension modeDimension
            applicationId "th.test.core"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        dev.java.srcDirs += 'src/dev/kotlin'
        prod.java.srcDirs += 'src/prod/kotlin'
        staging.java.srcDirs += 'src/staging/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}

dependencies {
    def ext = rootProject.ext
    implementation ext.support.multidex
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':core')
    implementation project(':main')
    implementation project(':loan')
    implementation project(':auth')
}